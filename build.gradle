subprojects { project ->
  group = GROUP
  version = VERSION_NAME
}

ext.kotlin_version = '1.0.6'

allprojects {
  repositories {
    jcenter()
  }

  buildscript {
    repositories {
      jcenter()
      maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
      classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
      classpath 'com.android.tools.build:gradle:2.2.3'
      classpath 'io.realm:realm-gradle-plugin:2.2.1'
      classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
      classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
      classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0'
      classpath 'org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.8'
    }
  }

  tasks.withType(JavaForkOptions) {
    // should improve memory on a 64bit JVM
    jvmArgs "-XX:+UseCompressedOops"

    // should avoid GradleWorkerMain to steal focus
    jvmArgs "-Djava.awt.headless=true"
    jvmArgs "-Dapple.awt.UIElement=true"
  }
}

subprojects {
  tasks.withType(Javadoc).all { enabled = false }
}

ext {
  minSdkVersion = 9
  targetSdkVersion = 25
  compileSdkVersion = 25
  buildToolsVersion = '25.0.1'
  sourceCompatibilityVersion = JavaVersion.VERSION_1_7
  targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

// Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
project.ext.isCi = "true".equals(System.getenv("CI"))
project.ext.preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
project.plugins.whenPluginAdded { plugin ->
  if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
    project.android.dexOptions.preDexLibraries =
        rootProject.ext.preDexEnabled && !rootProject.ext.isCi
  } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
    project.android.dexOptions.preDexLibraries =
        rootProject.ext.preDexEnabled && !rootProject.ext.isCi
  }
}

def androidSupportVersion = '25.0.1'

ext.deps = [
    android            : 'com.google.android:android:4.1.1.4',
    supportAnnotations : "com.android.support:support-annotations:$androidSupportVersion",
    appCompat          : "com.android.support:appcompat-v7:$androidSupportVersion",
    supportTestRunner  : 'com.android.support.test:runner:0.5',
    javapoet           : 'com.squareup:javapoet:1.8.0',
    autovalue          : 'com.google.auto.value:auto-value:1.3',
    autocommon         : 'com.google.auto:auto-common:0.8',
    autoservice        : 'com.google.auto.service:auto-service:1.0-rc2',
    javaFormat         : 'com.google.googlejavaformat:google-java-format:1.1',
    junit              : 'junit:junit:4.12',
    truth              : 'com.google.truth:truth:0.30',
    compiletesting     : 'com.google.testing.compile:compile-testing:0.10'
]

task clean(type: Delete) {
  delete rootProject.buildDir
}
