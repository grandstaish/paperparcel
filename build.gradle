subprojects { project ->
  group = GROUP
  version = VERSION_NAME
}

ext.kotlin_version = '1.1.60'

allprojects {
  repositories {
    jcenter()
    google()
  }

  buildscript {
    repositories {
      jcenter()
      google()
      maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
      classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
      classpath 'com.android.tools.build:gradle:3.0.1'
      classpath 'io.realm:realm-gradle-plugin:2.2.1'
      classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
      classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
      classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0'
      classpath 'org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.8'
    }
  }
}

subprojects {
  tasks.withType(Javadoc).all { enabled = false }
}

ext {
  minSdkVersion = 9
  targetSdkVersion = 27
  compileSdkVersion = 27
  buildToolsVersion = '26.0.2'
  sourceCompatibilityVersion = JavaVersion.VERSION_1_7
  targetCompatibilityVersion = JavaVersion.VERSION_1_7
}

// Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
project.ext.isCi = "true".equals(System.getenv("CI"))
project.ext.preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))
project.plugins.whenPluginAdded { plugin ->
  if ("com.android.build.gradle.AppPlugin".equals(plugin.class.name)) {
    project.android.dexOptions.preDexLibraries =
        rootProject.ext.preDexEnabled && !rootProject.ext.isCi
  } else if ("com.android.build.gradle.LibraryPlugin".equals(plugin.class.name)) {
    project.android.dexOptions.preDexLibraries =
        rootProject.ext.preDexEnabled && !rootProject.ext.isCi
  }
}

def androidSupportVersion = '27.0.1'
def gradleIncapHelperVersion = '0.2'
def autoserviceVersion = '1.0-rc5'

ext.deps = [
        android                   : 'com.google.android:android:4.1.1.4',
        supportAnnotations        : "com.android.support:support-annotations:$androidSupportVersion",
        appCompat                 : "com.android.support:appcompat-v7:$androidSupportVersion",
        design                    : "com.android.support:design:$androidSupportVersion",
        supportTestRunner         : 'com.android.support.test:runner:0.5',
        javapoet                  : 'com.squareup:javapoet:1.8.0',
        autovalueProcessor        : 'com.google.auto.value:auto-value:1.6.5',
        autovalueApi              : 'com.google.auto.value:auto-value-annotations:1.6.3',
        autocommon                : 'com.google.auto:auto-common:0.8',
        autoserviceProcessor      : "com.google.auto.service:auto-service:$autoserviceVersion",
        autoserviceApi            : "com.google.auto.service:auto-service-annotations:$autoserviceVersion",
        javaFormat                : 'com.google.googlejavaformat:google-java-format:1.1',
        junit                     : 'junit:junit:4.12',
        truth                     : 'com.google.truth:truth:0.30',
        compiletesting            : 'com.google.testing.compile:compile-testing:0.10',
        lombok                    : "org.projectlombok:lombok:1.16.16",
        gradleIncapHelperApi      : "net.ltgt.gradle.incap:incap:$gradleIncapHelperVersion",
        gradleIncapHelperProcessor: "net.ltgt.gradle.incap:incap-processor:$gradleIncapHelperVersion"
]

task clean(type: Delete) {
  delete rootProject.buildDir
}
